global int ctx_member;
global int ctx_member_exist;
global int ctx_member_shares;

slice load_member_slice(slice cs) impure {
    ctx_member_shares = cs~load_coins();
    return cs;
}

() load_member(int member) impure {
    var (cs, found) = ctx_members.udict_get?(256, member);
    ctx_member = member;
    if (found) {
        cs = load_member_slice(cs);
        cs.end_parse();
        ctx_member_exist = true;
    } else {
        ctx_member_shares = 0;
        ctx_member_exist = false;
    }
}

() store_member() impure {
    var shouldExist = (ctx_member_shares > 0);
    if ((~ shouldExist) & ctx_member_exist) {
        
        ;; Compiler crashes when single lined
        var (changed, _) = udict_delete?(ctx_members, 256, ctx_member);
        ctx_members = changed;

    } elseif (shouldExist) {

        var data = begin_cell()
            .store_coins(ctx_member_shares);
        
        ;; Compiler crashes when single lined
        var changed = udict_set_builder(ctx_members, 256, ctx_member, data);
        ctx_members = changed;
    }
}