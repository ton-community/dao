;; Commands
int op::proposal() asm "3241702467 PUSHINT";
int op::proposal::ok() asm "2391675301 PUSHINT";

int op::vote() asm "3047515073 PUSHINT";
int op::vote::ok() asm "1127259026 PUSHINT";

int op::execute() asm "2483002579 PUSHINT";
int op::execute::ok() asm "2514550856 PUSHINT";

int op::abort() asm "1558599333 PUSHINT";
int op::abort::ok() asm "3612777127 PUSHINT";

;; Proposals
int proposal::transaction() asm "1225918510 PUSHINT";
int proposal::resolution() asm "3070113962 PUSHINT";
int proposal::destroy() asm "1738944794 PUSHINT";
int proposal::upgrade() asm "2894775163 PUSHINT";
int proposal::params() asm "1658590374 PUSHINT";
int proposal::cancel() asm "3137658663 PUSHINT";
int proposal::allocate() asm "2105678790 PUSHINT";
int proposal::deallocate() asm "1146177121 PUSHINT";

;; Errors
int error::invalid_address() asm "70 PUSHINT";
int error::invalid_message() asm "72 PUSHINT";
int error::access_denied() asm "73 PUSHINT";
int error::internal_error() asm "74 PUSHINT";
int error::too_low_value() asm "76 PUSHINT";
int error::no_voting_power() asm "77 PUSHINT";

;; State
int vote::yes() asm "1 PUSHINT";
int vote::no() asm "0 PUSHINT";
int vote::abstain() asm "2 PUSHINT";

int state::pending() asm "0 PUSHINT";
int state::success() asm "1 PUSHINT";
int state::failure() asm "2 PUSHINT";
int state::executed() asm "3 PUSHINT";
int state::aborted() asm "4 PUSHINT";
int state::cancelled() asm "5 PUSHINT";

;; uint64 max value
int params::max_shares() inline {
    return 2 << 128;
}

;;
;; Coins
;;

int coins::1() asm "1000000000 PUSHINT";
int coins::100() asm "100000000000 PUSHINT";

;;
;; Basic workchain addresses
;;

int parse_work_addr(slice cs) {
  (int sender_wc, slice sender_addr) = parse_var_addr(cs);
  throw_unless(error::invalid_address(), 0 == sender_wc);
  return sender_addr~load_uint(256);
}

(slice) serialize_work_addr(int addr) {
   return (begin_cell()
     .store_uint(2, 2)  ;; Is std address
     .store_uint(0, 1)  ;; Non-unicast
     .store_uint(0, 8)  ;; Basic workchain
     .store_uint(addr, 256) ;; Address hash
   ).end_cell().begin_parse();
}

;;
;; Custom Commands
;;

builder store_coins(builder b, int x) asm "STGRAMS";
(slice, int) load_coins(slice s) asm( -> 1 0) "LDGRAMS";
builder store_builder(builder to, builder what) asm(what to) "STB";
builder store_builder_ref(builder to, builder what) asm(what to) "STBREFR";

;;
;; Message operations
;;

() send_std_message(
  slice to_addr,
  int value,
  int mode, 
  int op, 
  int query_id,
  builder content
) impure {

  var body = begin_cell()
      .store_uint(op, 32)
      .store_uint(query_id, 64)
      .store_builder(content)
  .end_cell();

  var msg = begin_cell()
    .store_uint(0x10, 6)
    .store_slice(to_addr)
    .store_coins(value)
    .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_ref(body)
  .end_cell();

  send_raw_message(msg, mode);
}

() send_empty_std_message(
  slice to_addr,
  int value,
  int mode, 
  int op, 
  int query_id
) impure {

  var body = begin_cell()
      .store_uint(op, 32)
      .store_uint(query_id, 64)
  .end_cell();

  var msg = begin_cell()
    .store_uint(0x10, 6)
    .store_slice(to_addr)
    .store_coins(value)
    .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_ref(body)
  .end_cell();

  send_raw_message(msg, mode);
}

() send_receipt_message(
  int to_addr,
  int op, 
  int query_id,
  builder content
) impure {
  send_std_message(serialize_work_addr(to_addr), 0, 64 + 2, op, query_id, content);
}

() send_empty_receipt_message(
  int to_addr,
  int op, 
  int query_id
) impure {
  send_empty_std_message(serialize_work_addr(to_addr), 0, 64 + 2, op, query_id);
}

() send_empty_receipt_message_remaining(
  int to_addr,
  int op, 
  int query_id
) impure {
  send_empty_std_message(serialize_work_addr(to_addr), 0, 128 + 2, op, query_id);
}

global int ctx_version;
global int ctx_code_version;
global int ctx_params_version;

global int ctx_members_shares;
global int ctx_members_total;
global cell ctx_members_dict;

global int ctx_proposals_seq;
global cell ctx_proposals_dict;
global cell ctx_proposals_ids;

global cell ctx_params;

global cell ctx_extras;

() load_base_data() impure {

    ;; Parse root
    var ds = get_data().begin_parse();
    ctx_version = ds~load_uint(16);
    ctx_code_version = ds~load_uint(16);
    ctx_params_version = ds~load_uint(16);
    cell membersRef = ds~load_ref();
    cell proposalsRef = ds~load_ref();
    ctx_params = ds~load_ref();
    ctx_extras = ds~load_dict();
    ds.end_parse();

    ;; Parse members
    ds = membersRef.begin_parse();
    ctx_members_shares = ds~load_coins();
    ctx_members_total = ds~load_uint(32);
    ctx_members_dict = ds~load_ref();
    ds.end_parse();

    ;; Parse proposals
    ds = proposalsRef.begin_parse();
    ctx_proposals_seq = ds~load_uint(32);
    ctx_proposals_dict = ds~load_dict();
    ctx_proposals_ids = ds~load_dict();
    ds.end_parse();
}

() store_base_data() impure {
    set_data(begin_cell()
        .store_uint(ctx_version, 16)
        .store_uint(ctx_code_version, 16)
        .store_uint(ctx_params_version, 16)
        .store_ref(begin_cell()
            .store_coins(ctx_members_shares)
            .store_uint(ctx_members_total, 32)
            .store_ref(ctx_members_dict)
        .end_cell())
        .store_ref(begin_cell()
            .store_uint(ctx_proposals_seq, 32)
            .store_dict(ctx_proposals_dict)
            .store_dict(ctx_proposals_ids)
        .end_cell())
        .store_ref(ctx_params)
        .store_dict(ctx_extras)
    .end_cell());
}

() storage_upgrade() impure method_id(1666) {
    ;; Nothing to do
}

;;
;; Members storage
;; NOTE: this storage have to store ONLY voting shares, do not add anything else here
;;       since it is copied during proposal creation
;;

int load_shares(cell dict, int member) {
    var (cs, found) = dict.udict_get?(256, member);
    int coins = 0;
    if (found) {
        slice cs2 = cs;
        cell rf = cs2~load_ref();
        cs2.end_parse();

        cs2 = rf.begin_parse();
        coins = cs2~load_coins();
        cs2.end_parse();
    }
    return coins;
}

cell store_shares(cell dict, int member, int shares) {
    if (shares > 0) {
        var data = begin_cell()
            .store_ref(begin_cell()
                .store_coins(shares)
            .end_cell());
        return udict_set_builder(dict, 256, member, data);
    } else {
        var (changed, _) = udict_delete?(dict, 256, member);
        return changed;
    }
}

global int ctx_proposal_id;
global int ctx_proposal_state;
global int ctx_proposal_voted_yes;
global int ctx_proposal_voted_no;
global int ctx_proposal_voted_abstain;

;; Params
global int ctx_proposal_author;
global int ctx_proposal_created;
global int ctx_proposal_treshold_success;
global int ctx_proposal_treshold_failure;
global cell ctx_proposal_metadata;

;; Votes
global cell ctx_proposal_votes;

;; Body
global cell ctx_proposal_body;

;; Members
global cell ctx_proposal_members;
global int ctx_proposal_members_total;
global int ctx_proposal_members_shares;
global cell ctx_proposal_params;

;;
;; Loading proposal
;;

() load_proposal(int id) impure {

    ;; Load proposal
    var (itm, found) = ctx_proposals_dict.udict_get?(32, id); ;; NOTE: udict_get_ref could break stack
    throw_unless(error::invalid_message(), found);
    var cl = itm~load_ref();
    itm.end_parse();
    var cs = cl.begin_parse();

    ;; Read
    ctx_proposal_id = id;
    throw_if(error::invalid_message(), cs~load_uint(1) != 0);
    ctx_proposal_state = cs~load_uint(4);
    ctx_proposal_voted_yes = cs~load_coins();
    ctx_proposal_voted_no = cs~load_coins();
    ctx_proposal_voted_abstain = cs~load_coins();
    cell params_cell = cs~load_ref();
    ctx_proposal_votes = cs~load_dict();
    ctx_proposal_body = cs~load_ref();
    cell members_cell = cs~load_ref();
    cs.end_parse();
    
    ;; Params
    cs = params_cell.begin_parse();
    ctx_proposal_author = cs~load_uint(256);
    ctx_proposal_created = cs~load_uint(32);
    ctx_proposal_treshold_success = cs~load_coins();
    ctx_proposal_treshold_failure = cs~load_coins();
    ctx_proposal_params = cs~load_ref();
    ctx_proposal_metadata = cs~load_ref();
    cs.end_parse();

    ;; Members
    cs = members_cell.begin_parse();
    ctx_proposal_members_shares = cs~load_coins();
    ctx_proposal_members_total = cs~load_coins();
    ctx_proposal_members = cs~load_ref();
    cs.end_parse();
}

() store_proposal() impure {

    ;; Serialize proposal
    var container = begin_cell()
        .store_uint(0, 1)
        .store_uint(ctx_proposal_state, 4)
        .store_coins(ctx_proposal_voted_yes)
        .store_coins(ctx_proposal_voted_no)
        .store_coins(ctx_proposal_voted_abstain)
        .store_ref(begin_cell()
            .store_uint(ctx_proposal_author, 256)
            .store_uint(ctx_proposal_created, 32)
            .store_coins(ctx_proposal_treshold_success)
            .store_coins(ctx_proposal_treshold_failure)
            .store_ref(ctx_proposal_params)
            .store_ref(ctx_proposal_metadata)
        .end_cell())
        .store_dict(ctx_proposal_votes)
        .store_ref(ctx_proposal_body)
        .store_ref(begin_cell()
            .store_coins(ctx_proposal_members_shares)
            .store_coins(ctx_proposal_members_total)
            .store_ref(ctx_proposal_members)
        .end_cell())
    .end_cell();

    ;; Store ref
    var data = begin_cell().store_ref(container);
    ctx_proposals_dict = udict_set_builder(ctx_proposals_dict, 32, ctx_proposal_id, data);
}

_ check_params(cell src) {
    var cs = src.begin_parse();

    ;; Serialization version
    var version = cs~load_uint(1);
    throw_unless(error::internal_error(), version == 0);

    ;; Shares and voting
    var allocated = cs~load_coins(); ;; Check allocated
    throw_unless(error::internal_error(), params::max_shares() >= allocated);
    var base = cs~load_coins(); ;; Base
    var success = cs~load_coins(); ;; Success
    var failure = cs~load_coins(); ;; Failure
    throw_unless(error::internal_error(), base >= success);
    throw_unless(error::internal_error(), base >= failure);

    ;; Proposal cost
    var proposalCost = cs~load_coins();
    throw_unless(error::internal_error(), proposalCost >= coins::1());
    throw_unless(error::internal_error(), proposalCost <= coins::100());

    ;; Check metadata
    if (cs~load_int(1)) {
        cs~load_ref();
    }

    cs.end_parse();
    return true;
}

_ load_proposal_cost(cell src) {
    var cs = src.begin_parse();

    ;; Serialization version
    var version = cs~load_uint(1);
    throw_unless(error::internal_error(), version == 0);

    ;; Allocated
    cs~load_coins();

    ;; Params
    cs~load_coins(); ;; Base
    cs~load_coins(); ;; Success
    cs~load_coins(); ;; Failure

    ;; Proposal cost
    return cs~load_coins();
}

_ load_total_shares_number(cell src) {
    var cs = src.begin_parse();

    ;; Serialization version
    var version = cs~load_uint(1);
    throw_unless(error::internal_error(), version == 0);

    var allocated = cs~load_coins(); ;; Check allocated
    throw_unless(error::internal_error(), params::max_shares() >= allocated);

    return allocated;
}

_ load_voting_params(cell src) {
     var cs = src.begin_parse();

    ;; Serialization version
    var version = cs~load_uint(1);
    throw_unless(error::internal_error(), version == 0);

    ;; Allocated
    cs~load_coins();

    ;; Params
    var base = cs~load_coins();
    var success = cs~load_coins();
    var failure = cs~load_coins();
    return (base, success, failure);
}

_ load_metadata(cell src) {
    var cs = src.begin_parse();
    
    var version = cs~load_uint(1);
    throw_unless(error::internal_error(), version == 0);
    cs~load_coins(); ;; Allocated
    cs~load_coins(); ;; Base
    cs~load_coins(); ;; Success
    cs~load_coins(); ;; Failure
    cs~load_coins(); ;; Proposal Price

    ;; Check metadata
    if (cs~load_int(1)) {
        return cs~load_ref();
    } else {
        return null();
    }
}


;;
;; Checks proposal body
;;

int check_proposal(cell proposal) {
    slice cs = proposal.begin_parse();
    int kind = cs~load_uint(32);

    ;; Transaction proposal
    if (kind == proposal::transaction()) {
        cs~load_msg_addr(); ;; Load target
        cs~load_coins(); ;; Load coins
        ;; Load maybe stateinit
        if (cs~load_int(1)) {
            cs~load_ref();
        }
        ;; Load maybe payload
        if (cs~load_int(1)) {
            cs~load_ref();
        }
        cs.end_parse();
        return true;
    }

    ;; Resolution proposal
    if (kind == proposal::resolution()) {
        cs.end_parse();
        return true;
    }

    ;; Destroy proposal
    if (kind == proposal::destroy()) {
        cs~load_msg_addr(); ;; Load target
        cs.end_parse();
        return true;
    }

    ;; Upgrade proposal
    if (kind == proposal::upgrade()) {
        var expectedVersion = cs~load_uint(16); ;; Load code version
        if (expectedVersion != ctx_code_version) {
            return false;
        } else {
            cs~load_ref(); ;; Load code ref
            cs.end_parse();
            return true;
        }
    }

    ;; Params proposal
    if (kind == proposal::params()) {
        var expectedVersion = cs~load_uint(16);
        if (expectedVersion != ctx_params_version) {
            return false;
        } else {
            var params = cs~load_ref();
            if (~ check_params(params)) {
                return false;
            }
            cs.end_parse();
            return true;
        }
    }

    ;; Cancel proposal
    if (kind == proposal::cancel()) {
        cs~load_uint(32);
        cs.end_parse();
        return true;
    }

    ;; Allocate proposal
    if (kind == proposal::allocate()) {
        cs~load_uint(256);
        cs~load_coins();
        cs.end_parse();
        return true;
    }

    ;; Deallocate proposal
    if (kind == proposal::deallocate()) {
        cs~load_uint(256);
        cs~load_coins();
        cs.end_parse();
        return true;
    }

    return false;
}

;;
;; Register proposal
;;

() register_proposal(int seq, cell proposal, cell metadata, int member) impure {

    ;; Load shares
    int shares = load_shares(ctx_members_dict, member);
    throw_unless(error::no_voting_power(), shares > 0);
    
    ;; Check seq
    throw_unless(error::invalid_message(), seq == ctx_proposals_seq);

    ;; Resolve tresholds
    var (treshold_base, treshold_success_src, treshold_failure_src) = load_voting_params(ctx_params);
    var treshold_success = (ctx_members_shares * treshold_success_src) / treshold_base;
    var treshold_failure = (ctx_members_shares * treshold_failure_src) / treshold_base;
    
    ;;
    ;; Prepare proposal data
    ;;

    ctx_proposal_id = seq;
    ctx_proposal_state = state::pending();
    ctx_proposal_voted_yes = 0;
    ctx_proposal_voted_no = 0;
    ctx_proposal_voted_abstain = 0;

    ctx_proposal_author = member;
    ctx_proposal_created = now();
    ctx_proposal_treshold_success = treshold_success;
    ctx_proposal_treshold_failure = treshold_failure;
    ctx_proposal_metadata = metadata;

    ctx_proposal_votes = null();
    ctx_proposal_body = proposal;

    ctx_proposal_members = ctx_members_dict;
    ctx_proposal_members_total = ctx_members_total;
    ctx_proposal_members_shares = ctx_members_shares;
    ctx_proposal_params = ctx_params;

    ;;
    ;; Increment counter
    ;; 
    
    ctx_proposals_seq = seq + 1;

    ;;
    ;; Persist
    ;; 

    store_proposal();
    store_base_data();
}

;;
;; Register vote
;;

() register_vote(int proposalId, int member, int vote) impure {

    ;; Load proposal
    load_proposal(proposalId);

    ;; Check proposal state
    throw_unless(error::invalid_message(), ctx_proposal_state == state::pending());

    ;; Check if already voted
    var (vs, vfound) = ctx_proposal_votes.udict_get?(256, member);
    throw_if(error::invalid_message(), vfound);

    ;; Check if had shares
    var shares = load_shares(ctx_proposal_members, member);
    throw_unless(error::no_voting_power(), shares > 0);

    ;; Register vote
    if (vote == vote::yes()) {
        ctx_proposal_voted_yes = ctx_proposal_voted_yes + shares;
    } elseif (vote == vote::no()) {
        ctx_proposal_voted_no = ctx_proposal_voted_no + shares;
    } elseif (vote == vote::abstain()) {
        ctx_proposal_voted_abstain = ctx_proposal_voted_abstain + shares;
    } else {
        throw(error::invalid_message());
    }
    var changed = udict_set_builder(ctx_proposal_votes, 256, member, begin_cell().store_uint(vote, 2));
    ctx_proposal_votes = changed;

    ;; Check if vote succeeded
    if (ctx_proposal_voted_yes >= ctx_proposal_treshold_success) {
        ctx_proposal_state = state::success();
    }

    ;; Check if vote failed
    if (ctx_proposal_voted_no >= ctx_proposal_treshold_failure) {
        ctx_proposal_state = state::failure();
    }

    ;; Check if vote won't be able to succeed: too many votes against or abstent
    if (ctx_proposal_voted_no + ctx_proposal_voted_abstain > ctx_proposal_members_shares - ctx_proposal_treshold_success) {
        ctx_proposal_state = state::failure();
    }

    ;; Persist
    store_proposal();
    store_base_data();
}

;;
;; Prepare execution
;;

cell prepare_execute(int proposalId, int member) impure {
    
    ;; Load proposal
    load_proposal(proposalId);

    ;; Check proposal state
    throw_unless(error::invalid_message(), ctx_proposal_state == state::success());

    ;; Check if can execute
    throw_unless(error::invalid_message(), ctx_proposal_author == member);

    ;; Update executed state
    ctx_proposal_state = state::executed();

    ;; Keep reference for proposal body
    cell res = ctx_proposal_body;

    ;; Persist
    store_proposal();
    store_base_data();

    return res;
}

;;
;; Abort execution
;;

() abort_proposal(int proposalId, int member) impure {
    
    ;; Load proposal
    load_proposal(proposalId);

    ;; Check proposal state
    throw_unless(error::invalid_message(), ctx_proposal_state == state::success());

    ;; Check if can execute
    throw_unless(error::invalid_message(), ctx_proposal_author == member);

    ;; Update executed state
    ctx_proposal_state = state::aborted();

    ;; Persist
    store_proposal();
    store_base_data();
}

;;
;; Cancel proposal
;;

() cancel_proposal(int proposalId) impure {
    
    ;; Load proposal
    load_proposal(proposalId);

    ;; Check proposal state
    throw_unless(error::invalid_message(), 
        (ctx_proposal_state == state::executed()) | (ctx_proposal_state == state::cancelled()) | (ctx_proposal_state == state::aborted())
    );

    ;; Update executed state
    ctx_proposal_state = state::cancelled();

    ;; Persist
    store_proposal();
    store_base_data();
}

;;
;; Allocate voting power
;;

() allocate_voting_power(int member, int shares) impure {

    ;; Check input
    throw_unless(error::internal_error(), shares > 0);

    ;; Check if shares available
    var totalShares = load_total_shares_number(ctx_params);
    var allocatedShares = ctx_members_total;
    throw_unless(error::internal_error(), totalShares >= allocatedShares + shares);

    ;; Update membership
    int existingShares = load_shares(ctx_members_dict, member);
    if (existingShares == 0) {
        ctx_members_total = ctx_members_total + 1;
    }
    ctx_members_shares = ctx_members_shares + shares;
    ctx_members_dict = store_shares(ctx_members_dict, member, existingShares + shares);

    ;; Persist
    store_base_data();
}

;;
;; Deallocate voting power
;;

() deallocate_voting_power(int member, int shares) impure {

    ;; Check input
    throw_unless(error::internal_error(), shares > 0);

    ;; Load existing
    int existingShares = load_shares(ctx_members_dict, member);
    throw_unless(error::internal_error(), existingShares >= shares);

    ;; Update membership
    if (existingShares == shares) {
        throw_unless(error::internal_error(), ctx_members_total > 0);
        ctx_members_total = ctx_members_total - 1;
    }
    ctx_members_shares = ctx_members_shares - shares;
    ctx_members_dict = store_shares(ctx_members_dict, member, existingShares - shares);

    ;; Persist
    store_base_data();
}

;;
;; Proposal execution
;; NOTE: execution silently fails on invalid proposal to mark proposal as executed instead of crashing transactions
;;       but execution fails if something is wrong
;;

() execute_proposal(cell proposal) impure {

    ;; Begin parsing
    slice cs = proposal.begin_parse();
    int kind = cs~load_uint(32);

    ;; Tansaction
    if (kind == proposal::transaction()) {

        ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000 -> 0x18

        ;; Basic message
        slice to_addr = cs~load_msg_addr();
        int value = cs~load_coins();
        var msg = begin_cell()
            .store_uint(0x18, 6)
            .store_slice(to_addr)
            .store_coins(value)
            .store_uint(0, 1 + 4 + 4 + 64 + 32);

        ;; State init
        if (cs~load_int(1)) {
            var stateInit = cs~load_ref();
            msg = msg.store_uint(1, 1);
            msg = msg.store_ref(stateInit);
        } else {
            msg = msg.store_uint(0, 1);
        }

        ;; Payload
        if (cs~load_int(1)) {
            var payload = cs~load_ref();
            msg = msg.store_uint(1, 1);
            msg = msg.store_ref(payload);
        } else {
            msg = msg.store_uint(0, 1);
        }

        ;; Send message
        send_raw_message(msg.end_cell(), 1 + 2);
    }

    ;; Destroy contract
    if (kind == proposal::destroy()) {

        ;; Create destroy message
        ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 010000 -> 0x10
        slice to_addr = cs~load_msg_addr();
        var msg = begin_cell()
            .store_uint(0x10, 6)
            .store_slice(to_addr)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1);

        ;; Send message
        send_raw_message(msg.end_cell(), 32 + 128);
    }

    ;; Upgrade contract
    if (kind == proposal::upgrade()) {
        var expectedVersion = cs~load_uint(16);
        var newCode = cs~load_ref();
        if (ctx_code_version == expectedVersion) {

            ;; Update code version
            ctx_code_version = ctx_code_version + 1;
            store_base_data();

            ;; Update code
            set_code(newCode);

            ;; Upgrade code
            set_c3(newCode.begin_parse().bless());
            storage_upgrade();
            throw(0);
        }
    }

    ;; Update metadata
    if (kind == proposal::params()) {
        var version = cs~load_uint(16);
        cell new_params = cs~load_ref();
        if (ctx_params_version == version) {
            if (check_params(new_params)) {
                ctx_params_version = ctx_params_version + 1;
                ctx_params = new_params;
                store_base_data();   
            }
        }
    }

    ;; Cancel proposal
    if (kind == proposal::cancel()) {
        var id = cs~load_uint(32);
        cancel_proposal(id);
    }

    ;; Allocate proposal
    if (kind == proposal::allocate()) {
        var member = cs~load_uint(256);
        var shares = cs~load_coins();
        allocate_voting_power(member, shares);
    }

    ;; Deallocate proposal
    if (kind == proposal::deallocate()) {
        var member = cs~load_uint(256);
        var shares = cs~load_coins();
        deallocate_voting_power(member, shares);
    }
}

_ get_version() method_id {
    return (0, 0, 7);
}

_ get_code_version() method_id {
    load_base_data();
    return ctx_code_version;
}

_ get_members_info() method_id {
    load_base_data();
    return (0, ctx_members_total, ctx_members_shares);
}

_ get_params() method_id {
    load_base_data();
    var total_shares = load_total_shares_number(ctx_params);
    var (base, successTreshold, failureTreshold) = load_voting_params(ctx_params);
    var metadata = load_metadata(ctx_params);
    return (0, total_shares, ctx_members_shares, base, successTreshold, failureTreshold, metadata, ctx_params_version);
}

_ get_params_raw() method_id {
    load_base_data();
    return (ctx_params, ctx_params_version);
}

;;
;; Proposals
;;

_ get_last_proposal_id() method_id {
    load_base_data();
    return ctx_proposals_seq - 1;
}

_ get_proposal(int id) method_id {
    load_base_data();
    load_proposal(id);
    
    return (0, 
        ctx_proposal_state, 
        ctx_proposal_voted_yes, 
        ctx_proposal_voted_no, 
        ctx_proposal_voted_abstain, 
        ctx_proposal_body, 
        ctx_proposal_author,
        ctx_proposal_created,
        ctx_proposal_treshold_success,
        ctx_proposal_treshold_failure,
        ctx_proposal_metadata
    );
}

_ get_proposal_vote(int id, slice addr) method_id {

    ;; Load address
    (int sender_wc, slice sender_addr) = parse_var_addr(addr);
    throw_unless(error::invalid_message(), sender_wc == 0);
    int member = sender_addr~load_uint(256);

    ;; Load data
    load_base_data();
    load_proposal(id);

    ;; Load existing vote
    int existingVote = -1;
    var (vs, vfound) = ctx_proposal_votes.udict_get?(256, member);
    if (vfound) {
        existingVote = vs~load_uint(2);
    }

    ;; Load voting power
    int votingPower = load_shares(ctx_proposal_members, member);

    return (
        0, 
        existingVote, 
        votingPower,
        ctx_proposal_members_shares,
        ctx_proposal_members_total
    );
}

;;
;; Membership
;;

_ get_member(slice addr) method_id {

    ;; Load address
    (int sender_wc, slice sender_addr) = parse_var_addr(addr);
    throw_unless(error::invalid_message(), sender_wc == 0);
    int member = sender_addr~load_uint(256);

    ;; Load data
    load_base_data();

    ;; Load share
    int shares = load_shares(ctx_members_dict, member);
    if (shares > 0) {
        return (1, shares);
    } else {
        return (0, 0);
    }
}

_ get_members(slice after, int limit) method_id {
    throw_unless(error::invalid_message(), limit > 0);
    throw_unless(error::invalid_message(), limit < 100);
    load_base_data();
    var member = parse_work_addr(after);
    var count = 0;
    var members = null();
    do {
        (member, var cs, var f) = ctx_members_dict.udict_get_next?(256, member);
        if (f) {

            ;; Parse
            cell rf = cs~load_ref();
            cs.end_parse();
            var cs2 = rf.begin_parse();
            var shares = cs2~load_coins();
            cs2.end_parse();

            ;; Members
            members = cons([serialize_work_addr(member), shares], members);

            ;; Increase counter
            count = count + 1;
        }
    } until ((~ f) | (count >= limit));
    return members;
}

;;
;; Contract
;;

_ supported_interfaces() method_id {
    return (
        123515602279859691144772641439386770278, ;; org.ton.introspection.v0
        235309305202366707147534501513037963513 ;; com.tonwhales.dao.v1
    );
}

() op_proposal(int member, int value, slice in_msg, int query_id) impure {

    ;; Params
    int proposalCost = load_proposal_cost(ctx_params);

    ;; Check input
    int seq = in_msg~load_uint(32);
    cell proposal = in_msg~load_ref();
    cell metadata = in_msg~load_ref();
    in_msg.end_parse();
    throw_unless(error::invalid_message(), check_proposal(proposal));
    throw_unless(error::invalid_message(), proposalCost <= value);

    ;; Register input
    register_proposal(seq, proposal, metadata, member);

    ;; Receipt
    var [balance, extra] = get_balance();
    raw_reserve(balance - value + proposalCost, 0 + 2); ;; Reserve existing balance + proposal cost
    send_empty_receipt_message_remaining(member, op::proposal::ok(), query_id);
}

() op_vote(int member, int value, slice in_msg, int query_id) impure {
    
    ;; Check input
    int proposalId = in_msg~load_uint(32);
    int vote = in_msg~load_uint(2);
    in_msg.end_parse();

    ;; Register vote
    register_vote(proposalId, member, vote);
    send_empty_receipt_message(member, op::vote::ok(), query_id);
}

() op_execute(int member, int value, slice in_msg, int query_id) impure {

    ;; Load proposal ID
    int proposalId = in_msg~load_uint(32);
    
    ;; End and validate
    in_msg.end_parse();

    ;; Prepare
    var proposal = prepare_execute(proposalId, member);
    send_empty_receipt_message(member, op::execute::ok(), query_id);

    ;; Commit (to ignore errors during execution)
    commit();

    ;; Execute
    execute_proposal(proposal);
}

() op_abort(int member, int value, slice in_msg, int query_id) impure {

    ;; Load proposal ID
    int proposalId = in_msg~load_uint(32);
    
    ;; End and validate
    in_msg.end_parse();

    ;; Cancel proposal
    abort_proposal(proposalId, member);
    send_empty_receipt_message(member, op::abort::ok(), query_id);
}

() recv_internal(int msg_value, cell in_msg_cell, slice in_msg) impure {

    ;; Prepare message context
    var cs = in_msg_cell.begin_parse();
    var flags = cs~load_uint(4);  ;; int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool
    slice s_addr = cs~load_msg_addr();
    (int sender_wc, slice sender_addr) = parse_var_addr(s_addr);

    ;; Bounced
    ;; DAO accepts all bounced messages in case of invalid 
    ;; message was sent by DAO
    if (flags & 1) {
        return ();
    }

    ;; Ignore messages from non-workchain
    if (sender_wc != 0) {
        return ();
    }

    ;; Load base data
    int member = sender_addr~load_uint(256);
    load_base_data();

    ;; Ignore invalid operations
    if (in_msg.slice_bits() < (32 + 64)) {
        return ();
    }

    ;; Parse operation
    int op = in_msg~load_uint(32);
    int query_id = in_msg~load_uint(64);
    if (op == op::proposal()) {
        op_proposal(member, msg_value, in_msg, query_id);
        return ();
    }
    if (op == op::vote()) {
        op_vote(member, msg_value, in_msg, query_id);
        return ();
    }
    if (op == op::execute()) {
        op_execute(member, msg_value, in_msg, query_id);
        return ();
    }
    if (op == op::abort()) {
        op_abort(member, msg_value, in_msg, query_id);
        return ();
    }

    ;; Ignore all invalid operations since DAO could vote to return coins to sender
    return ();
}

